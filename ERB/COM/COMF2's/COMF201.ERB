;=============================================================================
;유두 고문
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE201
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:201 > 0
	RETURN 0
;Ｂ계 확장 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 8) == 0
	RETURN 0
;Ｂ계 장착구 사용 중에는 불가
SIF TEQUIP:35 || TEQUIP:36
	RETURN 0
;ウェディングドレス · ゴスロリ着用中の場合기교4이상 필수
SIF (TEQUIP:59 == 19 || TEQUIP:59 == 20) && ABL:PLAYER:12 < 4
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM201
PRINTL 유두 고문
TSTR:0 = 유두 고문
CALL TRAIN_MESSAGE_COM201

DOWNBASE:0 += 10
DOWNBASE:1 += 80

SOURCE:15 = 60
SOURCE:30 = 80
SOURCE:12 = 140

;ABL:B감각을 본다
IF ABL:3 == 0
	SOURCE:3 = 60
	SOURCE:11 = 30
ELSEIF ABL:3 == 1
	SOURCE:3 = 300
	SOURCE:11 = 60
ELSEIF ABL:3 == 2
	SOURCE:3 = 600
	SOURCE:11 = 100
ELSEIF ABL:3 == 3
	SOURCE:3 = 1400
	SOURCE:11 = 120
ELSEIF ABL:3 == 4
	SOURCE:3 = 2200
	SOURCE:11 = 140
ELSE
	SOURCE:3 = 3000
	SOURCE:11 = 300
ENDIF

IF TALENT:134
	;ABL:분유중독을 본다
	IF ABL:34 == 0
		TIMES SOURCE:3 , 1.00
		TIMES SOURCE:11 , 1.00
	ELSEIF ABL:34 == 1
		TIMES SOURCE:3 , 1.20
		TIMES SOURCE:11 , 1.00
	ELSEIF ABL:34 == 2
		TIMES SOURCE:3 , 1.20
		TIMES SOURCE:11 , 1.20
	ELSEIF ABL:34 == 3
		TIMES SOURCE:3 , 1.40
		TIMES SOURCE:11 , 1.20
	ELSEIF ABL:34 == 4
		TIMES SOURCE:3 , 1.40
		TIMES SOURCE:11 , 1.40
	ELSE
		TIMES SOURCE:3 , 1.60
		TIMES SOURCE:11 , 1.60
	ENDIF
ENDIF

;음유
IF TALENT:174
	TIMES SOURCE:11 , 1.40
	TIMES SOURCE:30 , 0.80
	TIMES SOURCE:3 , 1.40
ENDIF

;조교자가［유아］,［유치］
IF TALENT:PLAYER:130 || TALENT:PLAYER:131
	TIMES SOURCE:3 , 1.20
	TIMES SOURCE:11 , 1.20
ENDIF
;조교자가［유아 퇴행］
SIF TALENT:PLAYER:159
	TIMES SOURCE:11 , 1.20

;노예의 B⇔조교자의 손가락의 불결이 이동
STAIN:5 |= STAIN:PLAYER:1
STAIN:PLAYER:1 |= STAIN:5

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 5
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 5
ENDIF

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM201
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
PRINTFORM %CALLNAME:TARGET%의 
IF TALENT:110
	PRINT 거의 없는 거나 다름없는 
ELSEIF TALENT:128 && TALENT:111
	PRINT 간신히 부풀어 오른 게 느껴지는 정도의 
ELSEIF TALENT:111
	PRINT 표준보다 약간 작은 
ELSEIF TALENT:112
	PRINT 커다랗고 곡선이 아름다운 
ELSEIF TALENT:113
	PRINT 감당하기 힘들 만큼 큰 
ENDIF
IF TEQUIP:68
	PRINT 가슴의 
	SIF EX:23
		PRINT 탱글탱글하게 응어리진 
	PRINTW 유두를, 손가락 끝으로 살짝 간질였다….
ELSEIF PALAM:13 > PALAMLV:3
	PRINT 가슴의 
	PRINTW 완전히 딱딱해진 돌기를 지긋이 짜냈다….
ELSE
	PRINT 가슴의 
	PRINTW 아직 부드러운 돌기를 지긋이 짜냈다….
ENDIF
