;=============================================================================
;곤충 채집 세트의 약
;=============================================================================
;………………………………………………
;실행 판정
;………………………………………………
@COM_ABLE421
;개별 설정 커맨드 필터
SIF DITEMTYPE:999:421 > 0
	RETURN 0
;캐릭터 전용 커맨드를 유효로 하지 않으면 불가
SIF (FLAG:15 & 16384) == 0
	RETURN 0
;조교자가 永琳じゃないとダメ
SIF NO:PLAYER != 29
	RETURN 0
;媚薬, 利尿剤, 排卵誘発剤, 強精神薬全てを最低1つずつ持っているか, アイテムなし設定じゃないとダメ
SIF ITEM:61 == 0 && ITEM:62 == 0 && ITEM:64 == 0 && ITEM:66 == 0 && NOITEM == 0
	RETURN 0
;체력이 1000미만では使用不可
SIF BASE:0 < 1000
	RETURN 0
;薬毒耐性だとダメ
SIF TALENT:44
	RETURN 0
;수면 중에는 불가
SIF EQUIP:14
	RETURN 0
;昆虫採集セットの薬効果発揮中は不可
SIF TEQUIP:19
	RETURN 0
;촉수 조교 중에는 불가
SIF TEQUIP:90
	RETURN 0
RETURN 1

;--------------------------------------------------
;커맨드 본체
;--------------------------------------------------
@COM421
PRINTL 곤충 채집 세트의 약
TSTR:0 = 곤충 채집 세트의 약

LOCAL:1 = RAND:16
TFLAG:99 = LOCAL:1
CALL TRAIN_MESSAGE_COM421

DOWNBASE:0 += 300
DOWNBASE:1 += 300

SOURCE:26 = 3500
SOURCE:31 = 7000
SOURCE:32 = 1500

LOCAL:2 = 1000
LOCAL:3 = 5000

IF LOCAL:1 == 0
	SOURCE:0 = LOCAL:3
	SIF TALENT:120 == 0
		SOURCE:1 = LOCAL:3
	SOURCE:2 = LOCAL:3
	SOURCE:18 = LOCAL:3
	SOURCE:12 = LOCAL:3
	SOURCE:3 = LOCAL:3
ELSEIF LOCAL:1 >= 1 && LOCAL:1 < 3 && TALENT:120 == 0
	IF ABL:1 <= 2
		SOURCE:1 = LOCAL:2
		SOURCE:12 = LOCAL:2
	ELSE
		SOURCE:1 = LOCAL:3
		SOURCE:12 = LOCAL:3
	ENDIF
ELSEIF LOCAL:1 >= 3 && LOCAL:1 < 5
	IF ABL:2 <= 2
		SOURCE:2 = LOCAL:2
		SOURCE:12 = LOCAL:2
	ELSE
		SOURCE:2 = LOCAL:3
		SOURCE:12 = LOCAL:3
	ENDIF
ELSEIF LOCAL:1 >= 5 && LOCAL:1 < 8
	IF ABL:3 <= 2
		SOURCE:3 = LOCAL:2
		SOURCE:12 = LOCAL:2
	ELSE
		SOURCE:3 = LOCAL:3
		SOURCE:12 = LOCAL:3
	ENDIF
ELSEIF LOCAL:1 >= 8 && LOCAL:1 < 11
	IF ABL:0 <= 2
		SOURCE:0 = LOCAL:2
		SOURCE:12 = LOCAL:2
	ELSE
		SOURCE:0 = LOCAL:3
		SOURCE:12 = LOCAL:3
	ENDIF
ELSEIF LOCAL:1 == 15 && TALENT:116 == 0
	PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% [동물 귀]를 얻었다. 
	TALENT:116 = 1
ELSE
	SOURCE:18 = 7000
	PALAM:17 += 4000
ENDIF

;미약, 이뇨제, 배란유도제, 현자의 피를 1개씩 소비
IF NOITEM == 0
	ITEM:61 -= 1
	ITEM:62 -= 1
	ITEM:64 -= 1
	ITEM:66 -= 1
	SIF FLAG:13 & 4 && MONEY:0 >= ITEMPRICE:61
		CALL FILL_PLURAL_ITEM, 61
	SIF FLAG:13 & 8 && MONEY:0 >= ITEMPRICE:62
		CALL FILL_PLURAL_ITEM, 62
	SIF FLAG:13 & 8 && MONEY:0 >= ITEMPRICE:64
		CALL FILL_PLURAL_ITEM, 64
	SIF FLAG:13 & 8 && MONEY:0 >= ITEMPRICE:66
		CALL FILL_PLURAL_ITEM, 66
ENDIF

PALAM:17 += 1000
TEQUIP:19 = 1

IF TALENT:120 == 0 && TALENT:PLAYER:120 == 0
	TCVAR:12 += 1
ELSEIF TALENT:120 && TALENT:PLAYER:120
	TCVAR:13 += 1
ENDIF

;약물 경험
TCVAR:40 += 3

RETURN 1

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@TRAIN_MESSAGE_COM421
CALL KOJO_TRAIN_MESSAGE_COM
SIF FLAG:10 == 0 || CFLAG:99 == 1 || RESULT != 0
	RETURN 0

PRINTL
PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% 
SIF TALENT:PLAYER:81 || TALENT:PLAYER:177 || ABL:PLAYER:20 > 2
	PRINT 가학적인 미소를 띠고, 싫어하는 
PRINTFORML %조사선택(CALLNAME:TARGET, "를")% 붙잡고는, 이상한 액체를 주입했다.
IF TFLAG:99 == 0
	PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 일순간 괴로운 듯한 표정을 지었지만, 다음 순간 강렬한 욱신거림을 온몸으로 느꼈다.
	PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 모든 성감대를 한 번에 자극된 듯한 착각에 빠졌다.
ELSEIF TFLAG:99 >= 1 && TFLAG:99 < 3 && TALENT:120 == 0
	PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
	SIF TALENT:0 == 1
		PRINT 처녀인 것에도 불구하고 
	PRINTW 자궁의 안쪽이 쑤시기 시작했다.
ELSEIF TFLAG:99 >= 3 && TFLAG:99 < 5
	PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
	SIF EXP:1 == 0
		PRINT 지금까지 뒤의 구멍을 조교 당한 적이 없었음에도 불구하고, 
	PRINTW 갑자기 항문을 쑤셔 뚫리고 싶은 이상한 충동에 사로잡혔다.
ELSEIF TFLAG:99 >= 5 && TFLAG:99 < 8
	PRINTFORM %CALLNAME:TARGET%의 
	IF TALENT:110 || TALENT:111
		PRINT 빈약한 가슴에 매달려 있는 콩 같은 유두가 
	ELSEIF TALENT:112 || TALENT:113
		PRINT 풍만한 언덕정상의 돌기가 
	ELSE
		PRINT 아름다운 유방의 끄트머리가 
	ENDIF
	PRINTW 갑자기 단단히 일어서기 시작했다.
ELSEIF TFLAG:99 >= 8 && TFLAG:99 < 11
	PRINTFORM %CALLNAME:TARGET%의 
	IF TALENT:120 || TALENT:121
		PRINTW 음경은 돌연 쑤시기 시작했다.
	ELSEIF TALENT:1
		PRINTW 갑자기 페니스와 같이 거대한 음핵이 쑤시기 시작했다.
	ELSE
		SIF TALENT:128
			PRINT 사랑스럽고 작은 
		PRINTW 음핵은 솟아올라 노출된 상태가 되었다.
	ENDIF
ELSEIF TFLAG:99 == 15 && TALENT:116 == 0
	PRINTFORM %CALLNAME:TARGET%의 머리에 갑자기 
	PRINTDATA LOCAL 
		DATA 강아지 귀
		DATA 고양이 귀
		DATA 여우귀
		DATA 토끼귀
		DATA 엘프 귀
	ENDDATA
	PRINTW 가 나 버렸다.
ELSE
	PRINTFORML %조사선택(CALLNAME:TARGET, "는")% 숨을 몰아 쉬면서 손톱이 팔에 박혀들 정도로 자신의 몸을 꽉 껴안고 있다.
	PRINTFORMW 그러나 몸의 뜨거움을 참을 수 없었는지, %CALLNAME:TARGET%의 발 밑에 애액의 웅덩이가 만들어지고 있다….
ENDIF
