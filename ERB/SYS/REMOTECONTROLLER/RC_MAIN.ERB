@RC_INIT
#DIM TRAINSIZE
#DIM CATEGORY_INDEX

VARSET RC_COMMAND_CATEGORY, 0
VARSET RC_COMMAND_PINNEDLIST, NULL

RC_COMMAND_CATEGORY:1 = 1

TRAINSIZE = VARSIZE("TRAINNAME")

REPEAT TRAINSIZE
    SIF TRAINNAME:COUNT == ""
        CONTINUE

    CATEGORY_INDEX = RC_COMMAND_CATEGORY:COUNT

    RC_COMMAND_PINNEDLIST:CATEGORY_INDEX:COUNT = TRUE
REND

@RC_UPDATE_LIST
#DIM TRAINSIZE
#DIM CATEGORY_INDEX

VARSET RC_COMMAND_LIST, NULL
VARSET RC_COMMAND_LIST_COUNT, 0

TRAINSIZE = VARSIZE("TRAINNAME")

REPEAT TRAINSIZE
    SIF TRAINNAME:COUNT == ""
        CONTINUE

    CATEGORY_INDEX = RC_COMMAND_CATEGORY:COUNT

    SIF RC_COMMAND_PINNEDLIST:CATEGORY_INDEX:COUNT == NULL
        CONTINUE

    RESULT = TRUE
    TRYCALLFORM COM_ABLE{COUNT}

    DEBUGPRINTFORML TRAIN [%TRAINNAME:COUNT%] = \@ RESULT ? "TRUE" # "FALSE" \@

    SIF RESULT
        RC_COMMAND_LIST:CATEGORY_INDEX:((RC_COMMAND_LIST_COUNT:CATEGORY_INDEX)++) = COUNT

REND


@RC_START
#DIM CATEGORY_INDEX
#DIM PAGE_INDEX
#DIM COMMAND_CURSOR
#DIM MAX_COMMAND_CURSOR
#DIM MAX_PAGE
#DIM COMMAND_LIST_INDEX_BASE
#DIM COMMAND_LIST_INDEX
#DIM PREV_LINECOUNT

CATEGORY_INDEX = 0
PAGE_INDEX = 0
COMMAND_CURSOR = 0

CALL RC_INIT
CALL RC_UPDATE_LIST

PREV_LINECOUNT = LINECOUNT

WHILE TRUE

    MAX_PAGE = RC_COMMAND_LIST_COUNT:CATEGORY_INDEX / RC_PAGE_SIZE
    COMMAND_LIST_INDEX_BASE = PAGE_INDEX * RC_PAGE_SIZE
    COMMAND_LIST_INDEX = COMMAND_LIST_INDEX_BASE + COMMAND_CURSOR

    MAX_COMMAND_CURSOR = MIN(RC_PAGE_SIZE, RC_COMMAND_LIST_COUNT:CATEGORY_INDEX - COMMAND_LIST_INDEX_BASE) - 1

    CALL SHOW_STATUS
    DRAWLINE

    REPEAT RC_CATEGORY_COUNT
        CALL RC_PRINTC(RC_CATEGORY_NAMES:COUNT, COUNT == CATEGORY_INDEX)
    REND

    PRINTL
    DRAWLINE

    FOR COUNT, COMMAND_LIST_INDEX_BASE, COMMAND_LIST_INDEX_BASE + RC_PAGE_SIZE

        LOCAL = RC_COMMAND_LIST:CATEGORY_INDEX:COUNT

        IF LOCAL != NULL
            CALL RC_PRINTL(TRAINNAME:LOCAL, COUNT == COMMAND_LIST_INDEX)
        ENDIF
    NEXT

    DRAWLINE

    PRINTFORML PAGE {PAGE_INDEX + 1} / {MAX_PAGE + 1}


    ONEINPUTS

    SELECTCASE RESULTS

        CASE "7"
            IF CATEGORY_INDEX > 0
                CATEGORY_INDEX -= 1
                PAGE_INDEX = 0
                COMMAND_CURSOR = 0
            ENDIF
        
        CASE "9"
            IF CATEGORY_INDEX < RC_CATEGORY_COUNT - 1
                CATEGORY_INDEX += 1
                PAGE_INDEX = 0
                COMMAND_CURSOR = 0
            ENDIF

        CASE "4"
            IF PAGE_INDEX > 0
                PAGE_INDEX -= 1
                COMMAND_CURSOR = 0
            ENDIF

        CASE "6"
            IF PAGE_INDEX < MAX_PAGE
                PAGE_INDEX += 1
                COMMAND_CURSOR = 0
            ENDIF

        CASE "8"
            SIF COMMAND_CURSOR > 0
                COMMAND_CURSOR -= 1
        
        CASE "2"
            SIF COMMAND_CURSOR < MAX_COMMAND_CURSOR
                COMMAND_CURSOR += 1

        CASE "5"
            IF RC_COMMAND_LIST:CATEGORY_INDEX:COMMAND_LIST_INDEX != NULL
                CALL EXECUTE_COMMAND(RC_COMMAND_LIST:CATEGORY_INDEX:COMMAND_LIST_INDEX)
                DEBUGPRINTL START UPDATE
                CALL RC_UPDATE_LIST
                DEBUGPRINTL END UPDATE
            ENDIF

        CASE "q", "Q"
            RETURN TRUE

    ENDSELECT

    CLEARLINE (LINECOUNT - PREV_LINECOUNT)
WEND

@RC_PRINTC(CONTENT, IS_SELECTED)
#DIMS CONTENT
#DIM IS_SELECTED

SIF IS_SELECTED
    CALL SETCOLOR_TORIKOMODE("YELLOW", 3)

PRINTFORMC %CONTENT%

RESETCOLOR

@RC_PRINTL(CONTENT, IS_SELECTED)
#DIMS CONTENT
#DIM IS_SELECTED

SIF IS_SELECTED
    CALL SETCOLOR_TORIKOMODE("YELLOW", 3)

PRINTFORML %CONTENT%

RESETCOLOR
