@RC_UPDATE_LIST
#DIM TRAINSIZE
#DIM CATEGORY_HASH

VARSET RC_COMMAND_LIST, NULL
VARSET RC_COMMAND_LIST_COUNT, 0

TRAINSIZE = VARSIZE("TRAINNAME")

REPEAT TRAINSIZE
    SIF TRAINNAME:COUNT == ""
        CONTINUE

    CATEGORY_HASH = RC_COMMAND_CATEGORY:COUNT

    RESULT = TRUE
    TRYCALLFORM COM_ABLE{COUNT}

    SIF RESULT
        RC_COMMAND_LIST:CATEGORY_HASH:((RC_COMMAND_LIST_COUNT:CATEGORY_HASH)++) = COUNT

REND

@RC_GET_SUBCATEGORY_COUNT(ARG)
#FUNCTION

LOCAL = 0

FOR COUNT, 0, RC_SUB_CATEGORY_COUNT
    SIF RC_CONTAIN_SUB_CATEGORYS:ARG:COUNT == NULL
        BREAK

    LOCAL += 1
NEXT

RETURNF LOCAL

@RC_START
#DIM CATEGORY_CURSOR
#DIM PAGE_CURSOR
#DIM SUB_CATEGORY_COUNT
#DIM SUB_CATEGORY_INDEX
#DIM SUB_CATEGORY_CURSOR
#DIM CURRENT_HASH
#DIM COMMAND_CURSOR
#DIM MAX_COMMAND_CURSOR
#DIM MAX_PAGE
#DIM COMMAND_LIST_INDEX_BASE
#DIM COMMAND_LIST_INDEX
#DIM PREV_LINECOUNT

CATEGORY_CURSOR = 0
SUB_CATEGORY_CURSOR = 0
PAGE_CURSOR = 0
COMMAND_CURSOR = 0

CALL RC_INIT_LIST
CALL RC_UPDATE_LIST

PREV_LINECOUNT = LINECOUNT

WHILE TRUE

    CURRENT_HASH = RC_GET_HASHCODE(CATEGORY_CURSOR, SUB_CATEGORY_CURSOR)
    MAX_PAGE = RC_COMMAND_LIST_COUNT:CURRENT_HASH / RC_PAGE_SIZE
    COMMAND_LIST_INDEX_BASE = PAGE_CURSOR * RC_PAGE_SIZE
    COMMAND_LIST_INDEX = COMMAND_LIST_INDEX_BASE + COMMAND_CURSOR
    SUB_CATEGORY_COUNT = RC_GET_SUBCATEGORY_COUNT(CATEGORY_CURSOR)
    SUB_CATEGORY_INDEX = RC_CONTAIN_SUB_CATEGORYS:CATEGORY_CURSOR:SUB_CATEGORY_CURSOR

    MAX_COMMAND_CURSOR = MIN(RC_PAGE_SIZE, RC_COMMAND_LIST_COUNT:CURRENT_HASH - COMMAND_LIST_INDEX_BASE) - 1

    CALL SHOW_STATUS

    PRINTL 선택(5) 위로이동(8) 아래로이동(2) 이전페이지(4) 다음페이지(6)
    PRINTL 이전분류(7) 다음분류(9) 이전소분류(1) 다음소분류(3) 커맨드정보(I) 나가기(Q)
    PRINTL

    DRAWLINE

    REPEAT RC_CATEGORY_COUNT
        CALL RC_PRINTC(RC_CATEGORY_NAMES:COUNT, COUNT == CATEGORY_CURSOR)
    REND

    PRINTL
    DRAWLINE

    REPEAT SUB_CATEGORY_COUNT
        CALL RC_PRINTC(RC_SUB_CATEGORY_NAMES:(RC_CONTAIN_SUB_CATEGORYS:CATEGORY_CURSOR:COUNT), COUNT == SUB_CATEGORY_CURSOR)
    REND

    PRINTL
    DRAWLINE

    FOR COUNT, COMMAND_LIST_INDEX_BASE, COMMAND_LIST_INDEX_BASE + RC_PAGE_SIZE

        LOCAL = RC_COMMAND_LIST:CURRENT_HASH:COUNT

        IF LOCAL != NULL
            CALL RC_PRINTL(TRAINNAME:LOCAL, COUNT == COMMAND_LIST_INDEX)
        ENDIF
    NEXT

    DRAWLINE

    PRINTFORML PAGE {PAGE_CURSOR + 1} / {MAX_PAGE + 1}


    ONEINPUTS

    SELECTCASE RESULTS

        CASE "1"
            IF SUB_CATEGORY_CURSOR > 0
                SUB_CATEGORY_CURSOR -= 1
                PAGE_CURSOR = 0
                COMMAND_CURSOR = 0
            ENDIF
        
        CASE "3"
            IF SUB_CATEGORY_CURSOR < SUB_CATEGORY_COUNT - 1
                SUB_CATEGORY_CURSOR += 1
                PAGE_CURSOR = 0
                COMMAND_CURSOR = 0
            ENDIF


        CASE "7"
            IF CATEGORY_CURSOR > 0
                CATEGORY_CURSOR -= 1
                SUB_CATEGORY_CURSOR = 0
                PAGE_CURSOR = 0
                COMMAND_CURSOR = 0
            ELSE
                CATEGORY_CURSOR = RC_CATEGORY_COUNT - 1
                SUB_CATEGORY_CURSOR = 0
                PAGE_CURSOR = 0
                COMMAND_CURSOR = 0
            ENDIF
        
        CASE "9"
            IF CATEGORY_CURSOR < RC_CATEGORY_COUNT - 1
                CATEGORY_CURSOR += 1
                SUB_CATEGORY_CURSOR = 0
                PAGE_CURSOR = 0
                COMMAND_CURSOR = 0
            ELSE
                CATEGORY_CURSOR = 0
                SUB_CATEGORY_CURSOR = 0
                PAGE_CURSOR = 0
                COMMAND_CURSOR = 0
            ENDIF

        CASE "4"
            IF PAGE_CURSOR > 0
                PAGE_CURSOR -= 1
                COMMAND_CURSOR = 0
            ENDIF

        CASE "6"
            IF PAGE_CURSOR < MAX_PAGE
                PAGE_CURSOR += 1
                COMMAND_CURSOR = 0
            ENDIF

        CASE "8"
            SIF COMMAND_CURSOR > 0
                COMMAND_CURSOR -= 1
        
        CASE "2"
            SIF COMMAND_CURSOR < MAX_COMMAND_CURSOR
                COMMAND_CURSOR += 1

        CASE "5"
            IF RC_COMMAND_LIST:SUB_CATEGORY_INDEX:COMMAND_LIST_INDEX != NULL
                CALL EXECUTE_COMMAND(RC_COMMAND_LIST:SUB_CATEGORY_INDEX:COMMAND_LIST_INDEX)
                DRAWLINE
                WAITANYKEY
                CALL RC_UPDATE_LIST
            ENDIF

        CASE "q", "Q"
            RETURN TRUE

        CASE "i", "I"
            DRAWLINE
            TRYCALLFORM DICTIONARY_TRAIN_COMMAND_{RC_COMMAND_LIST:SUB_CATEGORY_INDEX:COMMAND_LIST_INDEX}
            DRAWLINE
            WAITANYKEY

    ENDSELECT

    CLEARLINE (LINECOUNT - PREV_LINECOUNT)
WEND

@RC_PRINTC(CONTENT, IS_SELECTED)
#DIMS CONTENT
#DIM IS_SELECTED

SIF IS_SELECTED
    SETCOLOR 255, 255, 0

PRINTFORMC %CONTENT%

RESETCOLOR

@RC_PRINTL(CONTENT, IS_SELECTED)
#DIMS CONTENT
#DIM IS_SELECTED

SIF IS_SELECTED
    SETCOLOR 255, 255, 0

PRINTFORML %CONTENT%

RESETCOLOR
